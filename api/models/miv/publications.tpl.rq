PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX cite: <http://citationstyles.org/schema/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX experts: <http://experts.ucdavis.edu>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX purl: <http://purl.org/ontology/bibo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ucdrp: <http://experts.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vivo_inf: <http://vitro.mannlib.cornell.edu/default/vitro-kb-inf>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {

  ?pub a ?type;
       ?pub_cp ?o;
       cite:type ?cite_type;
       cite:page ?page;
       cite:venue ?venue;
       cite:container-title ?venue_title;
       cite:ISSN ?venue_issn;
       cite:issued ?issued;
       cite:citation-label ?pub_id;
       cite:author ?a;
       .

  ?a cite:rank ?rank;
     ?vname_cp ?vname_obj;
     .

} WHERE {

  bind (<{{publication}}> as ?pub)

  values(?pub_p ?pub_cp) {
    (rdfs:label cite:title)
    (bibo:issue cite:issue)
    (bibo:volume cite:volume)
    (bibo:abstract cite:abstract)
    (bibo:doi cite:DOI)
    (bibo:volume cite:volume)
    (bibo:pageStart cite:pageStart)
    (bibo:pageEnd cite:pageEnd)
  }

  values(?vname_p ?vname_cp) {
   (vcard:givenName cite:given)
   (vcard:middleName cite:middle)
   (vcard:familyName cite:family)
  }

  GRAPH ?g {
    bind(replace(str(?pub),str(<http://experts.ucdavis.edu/>),"") as ?pub_id)

    {
      select ?pub (max(?ct) as ?cite_type) WHERE {
	      values (?type ?ct ) {
    	  (bibo:AcademicArticle 'article-journal')
        (bibo:Book 'book')
        (bibo:Chapter 'chapter')
        (vivo:ConferencePaper 'paper-conference')
        (ucdrp:PrePrint 'article') }
        ?pub a ?type
      } group by ?pub
    }

    ?pub rdf:type ?type ;
      ?pub_p ?o ;
      vivo:relatedBy ?authorship .
     OPTIONAL {
       ?pub bibo:pageStart ?s ;
            bibo:pageEnd ?e .
       bind(concat(coalesce(?s,""),"-",coalesce(?e,"")) as ?page)
     }
    ?authorship a vivo:Authorship ;
        vivo:rank ?rank ;
        vivo:relates ?vcard .
    bind(uri(concat(str(?pub),'_',substr('0000',1,4-strlen(str(?rank))),str(?rank))) as ?a)
    ?vcard a vcard:Individual ; vcard:hasName ?vname .
    ?vname ?vname_p ?vname_obj .
    OPTIONAL {
      ?pub vivo:dateTimeValue ?issue.
      ?issue vivo:dateTime ?date.
      bind(strdt(replace(str(?date),"T.*","","i"),xsd:datetime) as ?issued)
    }
    OPTIONAL {
      ?pub vivo:hasPublicationVenue ?venue.
      ?venue rdfs:label ?venue_title.
      OPTIONAL { ?venue bibo:issn ?venue_issn. }
    }
    OPTIONAL {
      ?authorship vivo:relates ?person .
      ?person rdf:type foaf:Person .
      OPTIONAL { ?person vivo:orcidid ?authorOrcid . }
      OPTIONAL { ?person rdfs:label ?authorLabel . }
    }
  }
}
