PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucdrp: <http://experts.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX work: <http://experts.ucdavis.edu/work/>
PREFIX person: <http://experts.ucdavis.edu/person/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX CRediT: <https://credit.niso.org/contributor-roles/>


CONSTRUCT {
  ?expert rdf:type ?type;
  	rdf:type ucdrp:person;
    ucdrp:citation ?publication;
    # ucdrp:FirstorLastAuthor ?plus;
    .

  ?publication rdfs:label ?publicationLabel;
  	ucdrp:publication ?publicationVenueLabel;
    ucdrp:authors ?authorship ;
    vivo:hasSubjectArea ?subject;
    ucdrp:authorCount ?max;
    # ucdrp:FirstorLastAuthor ?FirstorLastAuthor;
    ucdrp:contrib-type ?FirstorLastAuthor ;
    .

  ?subject rdfs:label ?subject_label;
    skos:prefLabel ?subject_prefLabel;
    .

  ?authorship vivo:rank ?authorRank;
  	vivo:identifiers ?author;
    .
} WHERE {

  {select ?expert ?publication (max(?rank) as ?max) WHERE {
      ?expert rdf:type ucdrp:person;
              vivo:relatedBy/vivo:relates ?publication;
          .
      ?publication a ucdrp:work;
      	vivo:relatedBy/vivo:rank ?rank.                   
    } group by ?expert ?publication}
        
  ?expert rdf:type ?type;
          vivo:relatedBy ?authorship;
          .
    
    ?authorship rdf:type vivo:Authorship;
	    vivo:relates ?publication ;
                .
    
    ?publication rdf:type ucdrp:work;
    	rdfs:label ?publicationLabel;
     	.

  OPTIONAL {
      ?publication vivo:hasSubjectArea ?subject.
      ?subject rdfs:label ?subject_label.
      OPTIONAL {
        ?subject skos:prefLabel ?subject_prefLabel.
      }
    }	
  OPTIONAL {
      ?authorship vivo:relates ?publication;
                  vivo:rank ?authorRank.
      bind(?publication as ?plus)
      bind("ucdrp:FirstorLastAuthor" as ?FirstorLastAuthor)
      filter(?authorRank=?max || ?authorRank=1) 
    }

  OPTIONAL {
      ?publication vivo:hasPublicationVenue ?publicationVenue .
      ?publicationVenue rdfs:label ?publicationVenueLabel .
    }
  FILTER(
    ?expert = "{{uri}}"
  )
}
