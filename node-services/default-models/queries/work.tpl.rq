PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX for: <http://experts.ucdavis.edu/subject/FoR#>
PREFIX free: <http://experts.ucdavis.edu/subject/free#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ucdrp: <http://experts.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>

CONSTRUCT {
  ?subject rdf:type ?type, ucdrp:work ;
    rdfs:label ?label ;
    ucdrp:gcsMetadata ?gcsMetadata ;
    bibo:abstract ?abstract ;
    bibo:doi ?doi ;
    bibo:volume ?volume ;
    bibo:issue ?issue ;
    bibo:pageStart ?pageStart ;
    bibo:pageEnd ?pageEnd ;
    bibo:uri ?uri ;
    vivo:publicationDate ?dateTime ;
    .

  ?subject vivo:Authorship ?author .
  ?author rdf:type vivo:Authorship .
  ?author vivo:rank ?authorRank .

  ?author vivo:relates ?authorPerson .
  ?authorPerson vivo:orcid ?authorOrcid .
  ?authorPerson rdf:type ?authorPersonType .

  ?authorPerson vcard:hasName ?authorPersonVcardName .
  ?authorPersonVcardName vcard:Name ?authorPersonName .
  ?authorPersonVcardName vcard:givenName ?authorPersonGivenName .
  ?authorPersonVcardName vcard:middleName ?authorPersonMiddleName .
  ?authorPersonVcardName vcard:familyName ?authorPersonFamilyName .

  ?authorPerson vcard:hasName ?authorVcardName .
  ?authorVcardName vcard:Name ?authorName .
  ?authorVcardName vcard:givenName ?authorGivenName .
  ?authorVcardName vcard:middleName ?authorMiddleName .
  ?authorVcardName vcard:familyName ?authorFamilyName .

  ?subject obo:hasContactInfo ?hasContactInfo .
  ?hasContactInfo vcard:hasURL ?contactInfoHasUrl .
  ?contactInfoHasUrl vcard:url ?contactInfoUrl .
  ?contactInfoHasUrl vivo:rank ?contactInfoRank .
  ?contactInfoHasUrl rdfs:label ?contactInfoLabel .

  ?subject vivo:hasSubjectArea ?subjectArea .
  ?subjectArea rdfs:label ?subjectAreaLabel .
  ?subjectArea skos:prefLabel ?subjectAreaPrefLabel .

  ?subject vivo:hasPublicationVenue ?publicationVenue .

  ?publicationVenue
    rdfs:label ?publicationVenueLabel ;
    bibo:issn ?publicationVenueIssn ;
    bibo:eissn ?publicationVenueEissn ;
    bibo:isbn10 ?publicationIsbn10 ;
    bibo:isbn13 ?publicationIsbn13 ;
  .

  ?subject vivo:informationResourceSupportedBy ?supportedBy .
  ?supportedBy rdfs:label ?supportedByLabel .

} WHERE {

  ?subject rdf:type ?type .
  OPTIONAL { ?subject rdfs:label ?label . }
  OPTIONAL { ?subject ucdrp:gcsMetadata ?gcsMetadata . }
  OPTIONAL { ?subject bibo:abstract ?abstract . }
  OPTIONAL { ?subject bibo:doi ?doi . }
  OPTIONAL { ?subject bibo:volume ?volume . }
  OPTIONAL { ?subject bibo:pageStart ?pageStart . }
  OPTIONAL { ?subject bibo:pageEnd ?pageEnd . }
  OPTIONAL { ?subject bibo:uri ?uri . }

  OPTIONAL {
    ?subject vivo:relatedBy ?author .
    # ?author rdf:type vivo:Authorship .
    OPTIONAL { ?author vivo:rank ?authorRank . }
    OPTIONAL {
      ?author vivo:relates ?authorPerson .
      ?authorPerson rdf:type ?authorPersonType .

      OPTIONAL { ?authorPerson vivo:orcidid ?authorOrcid . }

      # in case the author is aggie experts
      OPTIONAL {
        ?authorPerson obo:ARG_2000028 ?authorPersonVcard .
        ?authorPersonVcard vcard:hasName ?authorPersonVcardName .
        OPTIONAL { ?authorPersonVcardName vcard:givenName ?authorPersonName . }
        OPTIONAL { ?authorPersonVcardName vcard:givenName ?authorPersonGivenName . }
        OPTIONAL { ?authorPersonVcardName vcard:middleName ?authorPersonMiddleName . }
        OPTIONAL { ?authorPersonVcardName vcard:familyName ?authorPersonFamilyName . }
      }

      # in case the author is just a vcard
      OPTIONAL {
        ?authorPerson vcard:hasName ?authorVcardName .
        OPTIONAL { ?authorVcardName vcard:Name ?authorName . }
        OPTIONAL { ?authorVcardName vcard:givenName ?authorGivenName . }
        OPTIONAL { ?authorVcardName vcard:middleName ?authorMiddleName . }
        OPTIONAL { ?authorVcardName vcard:familyName ?authorFamilyName . }
      }
    }
  }

  OPTIONAL {
    ?subject obo:ARG_2000028 ?hasContactInfo .
    ?hasContactInfo vcard:hasURL ?contactInfoHasUrl .
    OPTIONAL { ?contactInfoHasUrl vcard:url ?contactInfoUrl . }
    OPTIONAL { ?contactInfoHasUrl vivo:rank ?contactInfoRank . }
    OPTIONAL { ?contactInfoHasUrl rdfs:label ?contactInfoLabel . }
  }

  OPTIONAL {
    ?subject vivo:hasSubjectArea ?subjectArea .
    ?subjectArea rdfs:label ?subjectAreaLabel .
    OPTIONAL { ?subjectArea skos:prefLabel ?subjectAreaPrefLabel . }
  }

  OPTIONAL {
    ?subject vivo:hasPublicationVenue ?publicationVenue .
    ?publicationVenue
#       rdf:type ?publicationVenueType ;
       rdfs:label ?publicationVenueLabel .

    OPTIONAL {
      ?publicationVenue bibo:issn ?publicationVenueIssn .
    }
    OPTIONAL {
      ?publicationVenue bibo:eissn ?publicationVenueEissn .
    }
    OPTIONAL {
      ?publicationVenue bibo:isbn10 ?publicationIsbn10 .
    }
    OPTIONAL {
      ?publicationVenue bibo:isbn13 ?publicationIsbn13 .
    }
}

  OPTIONAL {
    ?subject vivo:informationResourceSupportedBy ?supportedBy .
    ?supportedBy rdfs:label ?supportedByLabel .
  }

  OPTIONAL {
    ?subject vivo:dateTimeValue ?publicationDate .
    OPTIONAL { ?publicationDate vivo:dateTime ?dateTime . }
  }

  FILTER(?subject = "{{uri}}" && ?authorPersonType IN (vcard:Individual, vivo:FacultyMember, vivo:NonAcademic, foaf:Person))

}
