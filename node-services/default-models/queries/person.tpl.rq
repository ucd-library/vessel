PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX for: <http://experts.ucdavis.edu/subject/FoR#>
PREFIX free: <http://experts.ucdavis.edu/subject/free#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ucdrp: <http://experts.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>

CONSTRUCT {
  ?subject rdf:type ?type .
  ?subject rdf:type ucdrp:person .

  ?subject rdfs:label ?label .
  ?subject vivo:scopusId ?scopusId .
  ?subject ucdrp:gcsMetadata ?gcsMetadata .
  ?subject vivo:researcherId ?researcherId .
  ?subject vivo:eRACommonsId ?eRACommonsId .
  ?subject vivo:overview ?overview .

  ?subject ucdrp:casId ?casId .
  ?subject ucdrp:identifier ?identifier .
  ?identifier rdf:type ucdrp:Identifier .
  ?identifier ucdrp:scheme ?identifierScheme .
  ?identifier ucdrp:value ?identifierValue .

  ?subject vivo:orcidId ?orcidId .
  ?orcidId vivo:confirmedOrcidId ?confirmedOrcidId .

  ?subject vivo:hasResearchArea ?hasResearchArea .
  ?hasResearchArea rdfs:label ?hasResearchAreaLabel .
  ?hasResearchArea skos:prefLabel ?hasResearchAreaPrefLabel .

  ?subject obo:hasContactInfo ?contactInfoFor .
  ?contactInfoFor vcard:givenName ?givenName .
  ?contactInfoFor vcard:familyName ?familyName .
  ?contactInfoFor vcard:middleName ?middleName .
  ?contactInfoFor vcard:pronoun ?pronoun .
  ?contactInfoFor vcard:additionalName ?additionalName .
  ?contactInfoFor vcard:honorificPrefix ?honorificPrefix .
  ?contactInfoFor vcard:honorificSuffix ?honorificSuffix .

  ?contactInfoFor vcard:hasEmail ?vcardEmail .
  ?vcardEmail vcard:email ?email .
  ?vcardEmail rdf:type ?emailType .

  ?contactInfoFor vcard:title ?title .
  ?contactInfoFor vcard:organization ?organization .
  ?contactInfoFor vcard:telephone ?telephone .
  ?contactInfoFor vcard:geo ?geo .
  ?contactInfoFor vivo:rank ?vcardRank .
  ?contactInfoFor ucdrp:identifier ?contactInfoForIdentifier .

  ?contactInfoFor vcard:hasURL ?vcardURL .

  ?vcardURL vcard:url ?url ;
     ucdrp:rank ?urlRank ;
  rdfs:label ?urlLabel ;
  ucdrp:urlType ?urlType;
  .


  ?subject vitro:mainImage ?mainImage .
  ?mainImage vitro:filename ?mainImagFilename .

  ?subject vivo:Position ?position .
  ?position rdfs:label ?positionLabel .
  ?position vivo:Organization ?positionOrg .
  ?positionOrg rdfs:label ?positionOrgLabel .
  ?positionOrg rdfs:start ?positionStartTimeValue .
  ?positionOrg rdfs:end ?positionEndTimeValue .

} WHERE {

    ?subject rdf:type ?type .
    OPTIONAL { ?subject rdfs:label ?label . }
    OPTIONAL { ?subject ucdrp:gcsMetadata ?gcsMetadata . }
    OPTIONAL { ?subject vivo:scopusId ?scopusId . }
    OPTIONAL { ?subject vivo:researcherId ?researcherId . }
    OPTIONAL { ?subject vivo:eRACommonsId ?eRACommonsId . }
    OPTIONAL { ?subject vivo:overview ?overview . }

    OPTIONAL { ?subject ucdrp:casId ?casId . }
    OPTIONAL {
      ?subject ucdrp:identifier ?identifier .
      OPTIONAL { ?identifier ucdrp:scheme ?identifierScheme . }
      OPTIONAL { ?identifier ucdrp:value ?identifierValue . }
    }

    OPTIONAL {
      ?subject vivo:orcidId ?orcidId .
      OPTIONAL { ?orcidId vivo:confirmedOrcidId ?confirmedOrcidId . }
    }

    OPTIONAL {
      ?subject vivo:hasResearchArea ?hasResearchArea .
      ?hasResearchArea rdfs:label ?hasResearchAreaLabel .
      OPTIONAL { ?hasResearchArea skos:prefLabel ?hasResearchAreaPrefLabel . }
    }

    OPTIONAL {
      ?subject obo:ARG_2000028 ?contactInfoFor .

      OPTIONAL {
        ?contactInfoFor vcard:hasName ?vcardName .
        OPTIONAL { ?vcardName vcard:givenName ?givenName . }
        OPTIONAL { ?vcardName vcard:familyName ?familyName . }
        OPTIONAL { ?vcardName vcard:middleName ?middleName . }
        OPTIONAL { ?vcardName vcard:pronoun ?pronoun .}
        OPTIONAL { ?vcardName vcard:additionalName ?additionalName . }
        OPTIONAL { ?vcardName vcard:honorificPrefix ?honorificPrefix . }
        OPTIONAL { ?vcardName vcard:honorificSuffix ?honorificSuffix . }
      }

      OPTIONAL { ?contactInfoFor vivo:rank ?vcardRank . }
      OPTIONAL { ?contactInfoFor ucdrp:identifier ?contactInfoForIdentifier . }

      OPTIONAL {
        ?contactInfoFor vcard:hasEmail ?vcardEmail .
        OPTIONAL { ?vcardEmail vcard:email ?email . }
        OPTIONAL { ?vcardEmail rdf:type ?emailType . }
      }

      OPTIONAL {
        ?contactInfoFor vcard:hasTitle ?vcardTitle .
        ?vcardTitle vcard:title ?title .
      }

      OPTIONAL {
        ?contactInfoFor vcard:hasOrganizationalUnit/vcard:title ?organization .
      }

      OPTIONAL {
        ?contactInfoFor vcard:hasTelephone ?vcardTelephone .
        ?vcardTelephone vcard:telephone ?telephone .
      }

      OPTIONAL {
        ?contactInfoFor vcard:hasGeo ?vcardGeo .
        ?vcardGeo vcard:geo ?geo .
      }

      OPTIONAL {
        ?contactInfoFor vcard:hasURL ?vcardURL .
        OPTIONAL { ?vcardURL vcard:url ?url . }
        OPTIONAL { ?vcardURL vcard:rank ?urlRank . }
        OPTIONAL { ?vcardURL rdfs:label ?urlLabel . }
        OPTIONAL { ?vcardURL ucdrp:urlType ?urlType . }
      }
    }

    OPTIONAL {
      ?subject vitro:mainImage ?mainImage .
      ?mainImage vitro:filename ?mainImagFilename .
    }

    OPTIONAL {
      ?subject vivo:relatedBy ?position .
      ?position rdf:type vivo:Position .

      OPTIONAL { ?position rdfs:label ?positionLabel . }

      OPTIONAL {
        ?position vivo:relates ?positionOrg .
        ?positionOrg rdfs:label ?positionOrgLabel .
      }

      OPTIONAL {
        ?position vivo:dateTimeInternal ?positionDateTime .
      }

      OPTIONAL {
        ?positionDateTime vivo:start ?positionStartTime .
        ?positionStartTime rdfs:label ?positionStartTimeValue .
      }

      OPTIONAL {
        ?positionDateTime vivo:end ?positionEndTime .
        ?positionEndTime rdfs:label ?positionEndTimeValue .
      }
    }

    FILTER( ?subject = "{{uri}}" )
}
