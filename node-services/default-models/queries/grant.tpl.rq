PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucdrp: <http://experts.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>

CONSTRUCT {

  ?subject rdf:type ?type;
           rdf:type ucdrp:grant;
           rdfs:label ?label;
           ucdrp:gcsMetadata ?gcsMetadata;
           vivo:sponsorAwardId ?sponsorAwardId;
           vivo:totalAwardAmount ?totalAwardAmount;
           vivo:assignedBy ?assignedBy;
           vivo:relates ?relates;
           vivo:dateTimeInterval ?dateTimeInterval;
         .


  ?assignedBy rdf:type ?assignedByType;
              rdfs:label ?assignedByLabel;
              .

  ?relates rdf:type ?relatesType;
           rdfs:label ?relates_label;
           obo:inheresIn ?inheresIn;
           obo:hasContactInfo ?relatesContactInfoFor;
           .

  ?relatesContactInfoFor vcard:hasName ?relatesVcardName;
                         .

  ?relatesVcardName vcard:givenName ?relatesGivenName;
                    vcard:familyName ?relatesFamilyName;
                    .

  ?inheresIn rdf:type ?inheresInType;
             obo:hasContactInfo ?inheresInContactInfoFor;
             .
  ?inheresInContactInfoFor vcard:hasName ?inheresInVcardName;
                           .

  ?inheresInVcardName vcard:givenName ?inheresInGivenName;
                      vcard:familyName ?inheresInFamilyName;
                      .

    ?dateTimeInterval rdf:type ?dateTimeIntervalType;
                      vivo:start ?dateTimeStart;
                      vivo:end ?dateTimeEnd;
                      .
  ?dateTimeStart rdf:type ?dateTimeStartType;
                 vivo:dateTime ?dateTimeStartTime;
                 vivo:dateTimePrecision ?dateTimeStartPrecision;
                 .

  ?dateTimeEnd rdf:type ?dateTimeEndType;
               vivo:dateTime ?dateTimeEndTime;
               vivo:dateTimePrecision ?dateTimeEndPrecision;
               .

} WHERE {
  ?subject rdf:type ?type .

  OPTIONAL { ?subject rdfs:label ?label . }
  OPTIONAL { ?subject ucdrp:gcsMetadata ?gcsMetadata . }
  OPTIONAL { ?subject vivo:sponsorAwardId ?sponsorAwardId . }
  OPTIONAL { ?subject vivo:totalAwardAmount ?totalAwardAmount . }

  OPTIONAL {
    ?subject vivo:assignedBy ?assignedBy .
    ?assignedBy rdf:type ?assignedByType .
    OPTIONAL { ?assignedBy rdfs:label ?assignedByLabel . }
  }

  OPTIONAL {
    ?subject vivo:relates ?relates .
    ?relates rdf:type ?relatesType .

    OPTIONAL {
      ?relates rdfs:label ?relates_label.
    }
    OPTIONAL {
      ?relates obo:ARG_2000028 ?relatesContactInfoFor .

      OPTIONAL {
        ?relatesContactInfoFor vcard:hasName ?relatesVcardName .
        OPTIONAL { ?relatesVcardName vcard:givenName ?relatesGivenName . }
        OPTIONAL { ?relatesVcardName vcard:familyName ?relatesFamilyName . }
      }
    }

    OPTIONAL {
      ?relates obo:RO_000052 ?inheresIn .
      ?inheresIn rdf:type ?inheresInType .

      OPTIONAL {
        ?inheresIn obo:ARG_2000028 ?inheresInContactInfoFor .

        OPTIONAL {
          ?inheresInContactInfoFor vcard:hasName ?inheresInVcardName .
          OPTIONAL { ?inheresInVcardName vcard:givenName ?inheresInGivenName . }
          OPTIONAL { ?inheresInVcardName vcard:familyName ?inheresInFamilyName . }
        }
      }
    }
  }

  OPTIONAL {
    ?subject vivo:dateTimeInterval ?dateTimeInterval .
    ?dateTimeInterval rdf:type ?dateTimeIntervalType .

    OPTIONAL {
      ?dateTimeInterval vivo:start ?dateTimeStart .
      ?dateTimeStart rdf:type ?dateTimeStartType .
      OPTIONAL { ?dateTimeStart vivo:dateTime ?dateTimeStartTime . }
      OPTIONAL { ?dateTimeStart vivo:dateTimePrecision ?dateTimeStartPrecision . }
    }

    OPTIONAL {
      ?dateTimeInterval vivo:end ?dateTimeEnd .
      ?dateTimeEnd rdf:type ?dateTimeEndType .
      OPTIONAL { ?dateTimeEnd vivo:dateTime ?dateTimeEndTime . }
      OPTIONAL { ?dateTimeEnd vivo:dateTimePrecision ?dateTimeEndPrecision . }
    }
  }

  FILTER(?subject = "{{uri}}")
}
